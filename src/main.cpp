#include <Arduino.h>
#include "hexapod.hpp"
#include "servo_controller.hpp"
#include "leg.hpp"
#include "ble_controller.hpp"
#include "trajectory.hpp"
#include "gait_controller.hpp"

BLEController ble;

Hexapod hexapod;
Leg testLeg(1, 0, 6, 5, 4); // Test leg 1: coxa=ch6, femur=ch5, tibia=ch4

// Bi·∫øn l∆∞u g√≥c hi·ªán t·∫°i cho test
int currentLeg = 0;
float currentX = 0;
float currentY = 0;
float currentZ = 0;
Leg currentLegs[6] = {
    Leg(0, 0, 2, 1, 0),  // Leg 0
    Leg(1, 0, 6, 5, 4),  // Leg 1
    Leg(2, 0, 10, 9, 8), // Leg 2
    Leg(3, 1, 2, 1, 0),  // Leg 3
    Leg(4, 1, 6, 5, 4),  // Leg 4
    Leg(5, 1, 10, 9, 8)  // Leg 5
};

// Bi·∫øn l∆∞u g√≥c servo hi·ªán t·∫°i cho ƒëi·ªÅu ch·ªânh tr·ª±c ti·∫øp
float currentServoAngles[6][3] = {
    {90.0f, 90.0f, 90.0f}, // Leg 0 (coxa, femur, tibia)
    {90.0f, 90.0f, 90.0f}, // Leg 1
    {90.0f, 90.0f, 90.0f}, // Leg 2
    {90.0f, 90.0f, 90.0f}, // Leg 3
    {90.0f, 90.0f, 90.0f}, // Leg 4
    {90.0f, 90.0f, 90.0f}  // Leg 5
};

// T√™n c·ªßa c√°c kh·ªõp
const char* jointNames[] = {"Coxa", "Femur", "Tibia"};


// C√°c khai b√°o h√†m (function prototypes)
void showTripodAngles(Leg legs[], const char *phase);
void testTripodGait();
void handlePosition();
void testServoAngles();
void chooseLeg();
void moveForward(Leg legs[], int tripod1[], int tripod2[]);
void handleBLECommand(char input);

void showTripodAngles(Leg legs[], const char *phase)
{
    Serial.print("  ");
    Serial.print(phase);
    Serial.println(":");

    // Show Tripod 1
    Serial.print("    Tripod 1 (0,2,4): ");
    for (int i : {0, 2, 4})
    {
        auto angles = legs[i].getServoAngles();
        Serial.print("L");
        Serial.print(i);
        Serial.print("(");
        Serial.print((int)angles[0]);
        Serial.print(",");
        Serial.print((int)angles[1]);
        Serial.print(",");
        Serial.print((int)angles[2]);
        Serial.print(") ");
    }
    Serial.println();

    // Show Tripod 2
    Serial.print("    Tripod 2 (1,3,5): ");
    for (int i : {1, 3, 5})
    {
        auto angles = legs[i].getServoAngles();
        Serial.print("L");
        Serial.print(i);
        Serial.print("(");
        Serial.print((int)angles[0]);
        Serial.print(",");
        Serial.print((int)angles[1]);
        Serial.print(",");
        Serial.print((int)angles[2]);
        Serial.print(") ");
    }
    Serial.println();
}

void testTripodGait()
{
    // Th√¥ng s·ªë di chuy·ªÉn
    const float x_home = 198.5f;        // X coordinate for all legs
    const float y_home = 0.0f;          // Y coordinate for all legs
    const float z_home = -50.1f;        // Z coordinate for all legs
    const int stepDelay = 400;          // Delay gi·ªØa c√°c b∆∞·ªõc (ms)
    const int UPDATE_INTERVAL = 15;     // C·∫≠p nh·∫≠t m·ªói 15ms ƒë·ªÉ c√≥ chuy·ªÉn ƒë·ªông m∆∞·ª£t
    const int STEPS = stepDelay / UPDATE_INTERVAL;
    
    Serial.println("\n=== TRIPOD GAIT TEST WITH SMOOTH TRAJECTORIES ===");
    Serial.println("Using Bezier curves for natural leg movement");
    Serial.println("Tripod 1: Legs 0, 2, 4 (ph·∫£i-tr∆∞·ªõc, ph·∫£i-sau, tr√°i-gi·ªØa)");
    Serial.println("Tripod 2: Legs 1, 3, 5 (ph·∫£i-gi·ªØa, tr√°i-tr∆∞·ªõc, tr√°i-sau)");
    Serial.println("Press any key to start walking sequence...");

    while (!Serial.available())
        delay(100);
    Serial.read();
    while (Serial.available())
        Serial.read();

    // Create all leg objects
    Leg legs[6] = {
        Leg(0, 0, 2, 1, 0),  // Leg 0
        Leg(1, 0, 6, 5, 4),  // Leg 1
        Leg(2, 0, 10, 9, 8), // Leg 2
        Leg(3, 1, 2, 1, 0),  // Leg 3
        Leg(4, 1, 6, 5, 4),  // Leg 4
        Leg(5, 1, 10, 9, 8)  // Leg 5
    };

    int tripod1[] = {0, 2, 4};
    int tripod2[] = {1, 3, 5};

    Serial.println("Starting tripod gait sequence with smooth trajectories...");

    // Walking cycles
    for (int cycle = 0; cycle < 3; cycle++)
    {
        Serial.print("\n=== WALKING CYCLE ");
        Serial.print(cycle + 1);
        Serial.println(" ===");

        // === TRIPOD 1 WALKING SEQUENCE (SMOOTH) ===
        Serial.println("TRIPOD 1 (0,2,4) WALKING WITH SMOOTH TRAJECTORY:");
        
        // Qu·ªπ ƒë·∫°o m∆∞·ª£t m√† cho tripod1
        for (int step = 0; step < STEPS; step++) {
            float t = (float)step / STEPS;  // 0.0 -> 1.0
            
            for (int i = 0; i < 3; i++) {
                float x, y, z;
                // S·ª≠ d·ª•ng qu·ªπ ƒë·∫°o Bezier v·ªõi gi√° tr·ªã m·∫∑c ƒë·ªãnh (ti·∫øn)
                Trajectory::createLegTrajectory(t, x, y, z, -150.0f);
                legs[tripod1[i]].setTargetPosition(x, y, z, t < 0.4f); // Elbow up for first half
                legs[tripod1[i]].update();
            }
            
            delay(UPDATE_INTERVAL);
        }

        showTripodAngles(legs, "Tripod 1 cycle completed");
        delay(100);  // Nh·ªè delay gi·ªØa c√°c chu k·ª≥

        // === TRIPOD 2 WALKING SEQUENCE (SMOOTH) ===
        Serial.println("TRIPOD 2 (1,3,5) WALKING WITH SMOOTH TRAJECTORY:");
        
        // Qu·ªπ ƒë·∫°o m∆∞·ª£t m√† cho tripod2
        for (int step = 0; step < STEPS; step++) {
            float t = (float)step / STEPS;
            
            for (int i = 0; i < 3; i++) {
                float x, y, z;
                Trajectory::createLegTrajectory(t, x, y, z, -150.0f);
                legs[tripod2[i]].setTargetPosition(x, y, z, t < 0.4f); // Elbow up for first half
                legs[tripod2[i]].update();
            }
            
            delay(UPDATE_INTERVAL);
        }

        showTripodAngles(legs, "Tripod 2 cycle completed");
        
        if (cycle < 2)
        {
            Serial.println("Press any key for next cycle...");
            while (!Serial.available())
                delay(100);
            Serial.read();
            while (Serial.available())
                Serial.read();
        }
    }

    // Quay tr·ªü v·ªÅ v·ªã tr√≠ home sau khi ho√†n th√†nh
    Serial.println("\nReturning to home position...");
    for (int i = 0; i < 6; i++)
    {
        legs[i].setTargetPosition(x_home, y_home, z_home);
        legs[i].update();
    }
    delay(500);

    Serial.println("\nüéâ SMOOTH TRIPOD GAIT TEST COMPLETED! üéâ");
}

void handlePosition()
{
    Serial.print("ü¶ø ƒêi·ªÅu ch·ªânh v·ªã tr√≠ ch√¢n ");
    Serial.print(currentLeg);
    Serial.println(" (a/s: X, d/f: Y, g/h: Z, q: Quay l·∫°i)");
    Serial.print("V·ªã tr√≠ hi·ªán t·∫°i: X=");
    Serial.print(currentX);
    Serial.print(", Y=");
    Serial.print(currentY);
    Serial.print(", Z=");
    Serial.println(currentZ);

    bool continueAdjusting = true;
    while (continueAdjusting)
    {
        while (Serial.available())
        {
            char input = Serial.read();
            switch (input)
            {
            case 'a':
                currentX += 1.0;
                Serial.print("X tƒÉng: ");
                Serial.println(currentX);
                break;
            case 's':
                currentX -= 1.0;
                Serial.print("X gi·∫£m: ");
                Serial.println(currentX);
                break;
            case 'd':
                currentY += 1.0;
                Serial.print("Y tƒÉng: ");
                Serial.println(currentY);
                break;
            case 'f':
                currentY -= 1.0;
                Serial.print("Y gi·∫£m: ");
                Serial.println(currentY);
                break;
            case 'g':
                currentZ += 1.0;
                Serial.print("Z tƒÉng: ");
                Serial.println(currentZ);
                break;
            case 'h':
                currentZ -= 1.0;
                Serial.print("Z gi·∫£m: ");
                Serial.println(currentZ);
                break;
            case 'q':
            case 'Q':
                continueAdjusting = false;
                Serial.println("Quay l·∫°i menu ch·ªçn ch√¢n");
                return;
            default:
                break;
            }

            Serial.print("ƒê·∫∑t v·ªã tr√≠ m·ªõi: X=");
            Serial.print(currentX);
            Serial.print(", Y=");
            Serial.print(currentY);
            Serial.print(", Z=");
            Serial.println(currentZ);

            currentLegs[currentLeg].setTargetPosition(currentX, currentY, currentZ);
            currentLegs[currentLeg].update();
        }
    }
}

void testServoAngles()
{
    Serial.print("üîÑ ƒêi·ªÅu ch·ªânh g√≥c servo ch√¢n ");
    Serial.print(currentLeg);
    Serial.println(" (q/w: Coxa, a/s: Femur, z/x: Tibia, r: Reset, e: Quay l·∫°i)");
    
    // Hi·ªÉn th·ªã g√≥c hi·ªán t·∫°i
    Serial.print("G√≥c hi·ªán t·∫°i: Coxa=");
    Serial.print(currentServoAngles[currentLeg][0]);
    Serial.print("¬∞, Femur=");
    Serial.print(currentServoAngles[currentLeg][1]);
    Serial.print("¬∞, Tibia=");
    Serial.print(currentServoAngles[currentLeg][2]);
    Serial.println("¬∞");


    bool continueAdjusting = true;
    while (continueAdjusting)
    {
        while (Serial.available())
        {
            char input = Serial.read();
            switch (input)
            {
            case 'q': // TƒÉng g√≥c Coxa
                currentServoAngles[currentLeg][0] += 1.0f;
                Serial.print("Coxa +1¬∞: ");
                Serial.println(currentServoAngles[currentLeg][0]);
                break;
            case 'w': // Gi·∫£m g√≥c Coxa
                currentServoAngles[currentLeg][0] -= 1.0f;
                Serial.print("Coxa -1¬∞: ");
                Serial.println(currentServoAngles[currentLeg][0]);
                break;
            case 'a': // TƒÉng g√≥c Femur
                currentServoAngles[currentLeg][1] += 1.0f;
                Serial.print("Femur +1¬∞: ");
                Serial.println(currentServoAngles[currentLeg][1]);
                break;
            case 's': // Gi·∫£m g√≥c Femur
                currentServoAngles[currentLeg][1] -= 1.0f;
                Serial.print("Femur -1¬∞: ");
                Serial.println(currentServoAngles[currentLeg][1]);
                break;
            case 'z': // TƒÉng g√≥c Tibia
                currentServoAngles[currentLeg][2] += 1.0f;
                Serial.print("Tibia +1¬∞: ");
                Serial.println(currentServoAngles[currentLeg][2]);
                break;
            case 'x': // Gi·∫£m g√≥c Tibia
                currentServoAngles[currentLeg][2] -= 1.0f;
                Serial.print("Tibia -1¬∞: ");
                Serial.println(currentServoAngles[currentLeg][2]);
                break;
            case 'Q': // TƒÉng g√≥c Coxa 5¬∞
                currentServoAngles[currentLeg][0] += 5.0f;
                Serial.print("Coxa +5¬∞: ");
                Serial.println(currentServoAngles[currentLeg][0]);
                break;
            case 'W': // Gi·∫£m g√≥c Coxa 5¬∞
                currentServoAngles[currentLeg][0] -= 5.0f;
                Serial.print("Coxa -5¬∞: ");
                Serial.println(currentServoAngles[currentLeg][0]);
                break;
            case 'A': // TƒÉng g√≥c Femur 5¬∞
                currentServoAngles[currentLeg][1] += 5.0f;
                Serial.print("Femur +5¬∞: ");
                Serial.println(currentServoAngles[currentLeg][1]);
                break;
            case 'S': // Gi·∫£m g√≥c Femur 5¬∞
                currentServoAngles[currentLeg][1] -= 5.0f;
                Serial.print("Femur -5¬∞: ");
                Serial.println(currentServoAngles[currentLeg][1]);
                break;
            case 'Z': // TƒÉng g√≥c Tibia 5¬∞
                currentServoAngles[currentLeg][2] += 5.0f;
                Serial.print("Tibia +5¬∞: ");
                Serial.println(currentServoAngles[currentLeg][2]);
                break;
            case 'X': // Gi·∫£m g√≥c Tibia 5¬∞
                currentServoAngles[currentLeg][2] -= 5.0f;
                Serial.print("Tibia -5¬∞: ");
                Serial.println(currentServoAngles[currentLeg][2]);
                break;
            case 'r': // Reset v·ªÅ 90¬∞
                currentServoAngles[currentLeg][0] = 90.0f;
                currentServoAngles[currentLeg][1] = 90.0f;
                currentServoAngles[currentLeg][2] = 90.0f;
                Serial.println("Reset g√≥c v·ªÅ 90¬∞");
                break;
            case 'h': // T√≠nh g√≥c HOME_CORRECTION t·ª´ g√≥c hi·ªán t·∫°i
                Serial.println("\n=== T√≠nh HOME_CORRECTION ===");
                Serial.print("Coxa_correction = ");
                Serial.print(currentServoAngles[currentLeg][0] - 90.0f);
                Serial.print(", Femur_correction = ");
                Serial.print(currentServoAngles[currentLeg][1] - 90.0f);
                Serial.print(", Tibia_correction = ");
                Serial.println(currentServoAngles[currentLeg][2] - 90.0f);
                Serial.println("Sao ch√©p gi√° tr·ªã n√†y v√†o m·∫£ng HOME_CORRECTION c·ªßa b·∫°n");
                break;
            case 'e':
                continueAdjusting = false;
                Serial.println("Quay l·∫°i menu ch·ªçn ch√¢n");
                return;
            default:
                break;
            }

            // Gi·ªõi h·∫°n g√≥c trong kho·∫£ng h·ª£p l·ªá (0-180¬∞)
            for (int j = 0; j < 3; j++) {
                currentServoAngles[currentLeg][j] = std::max(0.0f, std::min(180.0f, currentServoAngles[currentLeg][j]));
            }

            // √Åp d·ª•ng g√≥c m·ªõi
            int pca_id = currentLeg / 3;  // 0 cho ch√¢n 0-2, 1 cho ch√¢n 3-5
            
            // X√°c ƒë·ªãnh c√°c k√™nh servo cho t·ª´ng ch√¢n
            int channels[3];
            if (currentLeg % 3 == 0) {         // Ch√¢n 0 ho·∫∑c 3
                channels[0] = 2;  // coxa
                channels[1] = 1;  // femur
                channels[2] = 0;  // tibia
            } else if (currentLeg % 3 == 1) {  // Ch√¢n 1 ho·∫∑c 4
                channels[0] = 6;  // coxa
                channels[1] = 5;  // femur
                channels[2] = 4;  // tibia
            } else {                           // Ch√¢n 2 ho·∫∑c 5
                channels[0] = 10; // coxa
                channels[1] = 9;  // femur
                channels[2] = 8;  // tibia
            }
            
            // √Åp d·ª•ng g√≥c m·ªõi tr·ª±c ti·∫øp cho servo
            for (int j = 0; j < 3; j++) {
                ServoController::setAngle(pca_id, channels[j], currentServoAngles[currentLeg][j]);
            }
            
            // Hi·ªÉn th·ªã g√≥c hi·ªán t·∫°i c·ªßa t·∫•t c·∫£ c√°c kh·ªõp
            Serial.print("G√≥c hi·ªán t·∫°i: Coxa=");
            Serial.print(currentServoAngles[currentLeg][0]);
            Serial.print("¬∞, Femur=");
            Serial.print(currentServoAngles[currentLeg][1]);
            Serial.print("¬∞, Tibia=");
            Serial.print(currentServoAngles[currentLeg][2]);
            Serial.println("¬∞");
            Kinematics::computeFKArray(currentLeg, 
                currentServoAngles[currentLeg][0], 
                currentServoAngles[currentLeg][1], 
                currentServoAngles[currentLeg][2]);
        }
    }
}

// S·ª≠a ƒë·ªïi h√†m chooseLeg ƒë·ªÉ th√™m t√πy ch·ªçn ch·ªçn ph∆∞∆°ng ph√°p ƒëi·ªÅu ch·ªânh
void chooseLeg()
{
    Serial.println("\n===== CH·ªåN CH√ÇN ƒê·ªÇ ƒêI·ªÄU CH·ªàNH =====");
    Serial.println("Nh·∫•n s·ªë t·ª´ 0-5 ƒë·ªÉ ch·ªçn ch√¢n t∆∞∆°ng ·ª©ng:");
    Serial.println("  0: Ch√¢n ph·∫£i-tr∆∞·ªõc");
    Serial.println("  1: Ch√¢n ph·∫£i-gi·ªØa");
    Serial.println("  2: Ch√¢n ph·∫£i-sau");
    Serial.println("  3: Ch√¢n tr√°i-tr∆∞·ªõc");
    Serial.println("  4: Ch√¢n tr√°i-gi·ªØa");
    Serial.println("  5: Ch√¢n tr√°i-sau");
    Serial.println("  q: Quay l·∫°i menu ch√≠nh");

    bool selecting = true;

    while (selecting)
    {
        while (Serial.available())
        {
            char input = Serial.read();
            switch (input)
            {
            case '0':
            case '1':
            case '2':
            case '3':
            case '4':
            case '5':
            {
                currentLeg = input - '0';
                Serial.print("ƒê√£ ch·ªçn ch√¢n ");
                Serial.print(currentLeg);
                Serial.println(" ƒë·ªÉ ƒëi·ªÅu ch·ªânh.");
                
                Serial.println("\nCh·ªçn ph∆∞∆°ng ph√°p ƒëi·ªÅu ch·ªânh:");
                Serial.println("  1: ƒêi·ªÅu ch·ªânh v·ªã tr√≠ (X,Y,Z)");
                Serial.println("  2: ƒêi·ªÅu ch·ªânh g√≥c servo tr·ª±c ti·∫øp");
                Serial.println("  q: Quay l·∫°i");
                
                bool choosingMethod = true;
                while (choosingMethod) {
                    if (Serial.available()) {
                        char methodInput = Serial.read();
                        switch (methodInput) {
                            case '1':
                                // ƒê·ªçc v·ªã tr√≠ hi·ªán t·∫°i c·ªßa ch√¢n t·ª´ target_x, target_y, target_z
                                {
                                    auto position = currentLegs[currentLeg].getTargetPosition();
                                    currentX = position[0];
                                    currentY = position[1];
                                    currentZ = position[2];
                                    
                                    handlePosition();
                                    choosingMethod = false;
                                }
                                break;
                            case '2':
                                // ƒê·ªçc g√≥c servo hi·ªán t·∫°i
                                {
                                    auto angles = currentLegs[currentLeg].getServoAngles();
                                    currentServoAngles[currentLeg][0] = angles[0];
                                    currentServoAngles[currentLeg][1] = angles[1];
                                    currentServoAngles[currentLeg][2] = angles[2];
                                    
                                    testServoAngles();
                                    choosingMethod = false;
                                }
                                break;
                            case 'q':
                            case 'Q':
                                choosingMethod = false;
                                break;
                            default:
                                Serial.println("L·ª±a ch·ªçn kh√¥ng h·ª£p l·ªá. Vui l√≤ng ch·ªçn l·∫°i.");
                                break;
                        }
                    }
                }
            }
            break;
            case 'q':
            case 'Q':
                selecting = false;
                Serial.println("Quay l·∫°i menu ch√≠nh");
                return;
            default:
                Serial.println("L·ª±a ch·ªçn kh√¥ng h·ª£p l·ªá. Vui l√≤ng ch·ªçn l·∫°i.");
                break;
            }
        }
    }
}

void handleBLECommand(char input)
{
    Serial.print("BLE Command: ");
    Serial.println(input);

    switch (input)
    {
    case 'r':
        Serial.println("L·ªánh: V·ªÅ home position");
        hexapod.goHome();
        break;
    case 'm':
        Serial.println("L·ªánh: Test tripod gait");
        testTripodGait();
        break;
    case 'h':
        Serial.println("L·ªánh: Ch·ªçn ch√¢n ƒë·ªÉ ƒëi·ªÅu ch·ªânh");
        chooseLeg();
        break;
    default:
        Serial.println("L·ªánh kh√¥ng h·ª£p l·ªá! Nh·∫•n 'h' ƒë·ªÉ hi·ªÉn th·ªã tr·ª£ gi√∫p.");
        break;
    }
}

void setup()
{
    Serial.begin(115200);
    ble.init("Hexapod_BLE");
    ble.enableAutoConnect(true);
    ble.setCommandHandler(handleBLECommand);

    ServoController::initialize();
    delay(1000);

    hexapod.goHome();
}

void loop()
{
    ble.loop();

    static unsigned long lastStatusUpdate = 0;
    if (millis() - lastStatusUpdate > 10000)
    { // M·ªói 10 gi√¢y
        if (ble.isConnected())
        {
            // G·ª≠i th√¥ng b√°o tr·∫°ng th√°i khi ƒë√£ k·∫øt n·ªëi
            ble.sendMessage("STATUS:CONNECTED");
        }
        lastStatusUpdate = millis();
    }

    // X·ª≠ l√Ω d·ªØ li·ªáu joystick theo th·ªùi gian th·ª±c
    static unsigned long lastJoystickUpdate = 0;
    static unsigned long lastJoystickActivity = 0;
    static bool isMoving = false;
    if (ble.hasJoystickData() && millis() - lastJoystickUpdate > 50)
    { // TƒÉng l√™n 2Hz
        float joyX = ble.getJoystickX();
        float joyY = ble.getJoystickY();

        // Debug
        if (abs(joyX) > 0.1f || abs(joyY) > 0.1f)
        {
            Serial.print("Joy: X=");
            Serial.print(joyX);
            Serial.print(", Y=");
            Serial.println(joyY);
            // ƒê√°nh d·∫•u robot ƒëang di chuy·ªÉn
            isMoving = true;
            lastJoystickActivity = millis();
        }
        else if (isMoving)
        {
            // D·ª´ng robot khi joystick v·ªÅ trung t√¢m
            Serial.println("Joystick centered, stopping movement");
            hexapod.goHome();
            isMoving = false;
        }

        // Di chuy·ªÉn robot theo joystick
        hexapod.moveWithJoystick(joyX, joyY, false);
        lastJoystickUpdate = millis();
    }

    // Ki·ªÉm tra timeout: n·∫øu kh√¥ng nh·∫≠n ƒë∆∞·ª£c d·ªØ li·ªáu trong 500ms v√† robot ƒëang di chuy·ªÉn
    if (isMoving && (millis() - lastJoystickActivity > 300))
    {
        Serial.println("Joystick timeout, stopping movement");
        hexapod.goHome();
        isMoving = false;
    }

    // X·ª≠ l√Ω l·ªánh ƒë∆°n k√Ω t·ª± (cho c√°c ch·ª©c nƒÉng kh√°c)
    if (ble.hasCommand())
    {
        char cmd = ble.getCommand();
        if (cmd != 0)
        {
            handleBLECommand(cmd);
        }
    }

    // X·ª≠ l√Ω l·ªánh t·ª´ Serial (debugging)
    if (Serial.available())
    {
        char input = Serial.read();
        handleBLECommand(input);
    }
}

